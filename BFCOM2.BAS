5 CLS:CLEAR:MAXFILES=2
10 ON ERROR GOTO 10000
20 OPEN "A:PROG.BF" FOR INPUT AS #1
30 OPEN "A:PROG.ASM" FOR OUTPUT AS #2
35 GOSUB 2000
40 IF EOF(1) THEN GOTO 150
50 A$=INPUT$(1,1)
60 IF A$=">" THEN GOSUB 300:PRINTA$;:GOTO 40 
70 IF A$="<" THEN GOSUB 400:PRINTA$;:GOTO 40
80 IF A$="+" THEN GOSUB 500:PRINTA$;:GOTO 40
90 IF A$="-" THEN GOSUB 600:PRINTA$;:GOTO 40
100 IF A$="." THEN GOSUB 700:PRINTA$;:GOTO 40
110 IF A$="," THEN GOSUB 800:PRINTA$;:GOTO 40
120 IF A$="[" THEN GOSUB 900:PRINTA$;:GOTO 40
130 IF A$="]" THEN GOSUB 1000:PRINTA$;:GOTO 40
140 GOTO 40 
150 IF AN<0 THEN ERROR 999
160 IF AN>0 THEN ERROR 998
180 POKED1,&HC9:PRINT#2,,"RET","C9"
190 CLOSE#1:CLOSE#2
200 BSAVE "A:"+N$,DI,D1,DI+1024
210 PRINT:PRINT "INICIO:";HEX$(DI)
220 PRINT "EJECUCION:";HEX$(DI+1024)
230 PRINT "FINAL:";HEX$(D1)
299 END
300 REM INCREMENTA PUNTERO
310 PRINT#2,A$,HEX$(D1),"INC HL","23"
320 POKE D1,&H23:L=L+1:D1=D1+1
399 RETURN
400 REM DECREMENTA PUNTERO
410 PRINT#2,A$,HEX$(D1),"DEC HL","2B"
420 POKE D1,&H2B:L=L+1:D1=D1+1
499 RETURN
500 REM INCREMENTA VALOR
510 PRINT#2,A$,HEX$(D1),"INC (HL)","34"
520 POKE D1,&H34:D1=D1+1:L=L+1
599 RETURN
600 REM DECREMENTA VALOR
610 PRINT#2,A$,HEX$(D1),"DEC (HL)","35"
620 POKE D1,&H35:D1=D1+1:L=L+1
699 RETURN
700 REM SALIDA 
710 RESTORE 9020
720 PRINT#2,A$,HEX$(D1),"LD A,(HL)","7E":PRINT#2,,,"CP 10","FE 0D"
730 PRINT#2,,,"JR NZ,$+7","20 05":PRINT#2,,,"CALL CHPUT","CD A2 00"
740 PRINT #2,,,"LD A,13","3E 0D":PRINT#2,,,"CALL CHPUT","CD A2 00"
750 FOR I=1 TO 13:READ V
760 POKE D1,V:D1=D1+1
770 NEXT I
780 L=L+1
799 RETURN
800 REM  ****** ENTRADA  ***** TECLADO
810 RESTORE 9000
820 PRINT#2,A$,HEX$(D1),"CALL CHGET","CD 9F 00":PRINT#2,,,"CP 27","FE 1B"
830 PRINT#2,,,"JR NZ","20 01":PRINT#2,,,"XOR A","AF":PRINT#2,,,"LD (HL),A",77"
840 FOR I=1 TO 9:READ V
850 POKE D1,V:D1=D1+1
860 NEXT I
870 L=L+1
899 RETURN
900 REM CORCHETE ENTRADA
910 PRINT#2,A$,HEX$(D1),"XOR A","AF":PRINT#2,,,"CP (HL)","BE":PRINT#2,,"JP Z,";AN,"CA YY XX"
920 POKE D1,&HAF:D1=D1+1:POKE D1,&HBE:D1=D1+1:POKED1,&HCA:D1=D1+1
930 AN=AN+1:CO(AN)=D1:D1=D1+2
998 L=L+1
999 RETURN
1000 REM CORCHETE SALIDA
1010 PRINT#2,A$,HEX$(D1),"XOR A","AF":PRINT#2,,,"CP (HL)","BE"
1020 POKE D1,&HAF:D1=D1+1:POKE D1,&HBE:D1=D1+1:POKE D1,&HC2:D1=D1+1
1030 DC=CO(AN):DL=(DC+2) AND &HFF:DH=(DC+2)/256-&HFF00
1035 PRINT#2,,"JP NZ,";AN-1,"C2 ";HEX$(DL);" ";HEX$(DH)
1040 POKE D1,DL:D1=D1+1:POKE D1,DH:D1=D1+1
1050 DL=D1 AND &HFF:DH=D1/256-&HFF00
1060 POKE DC,DL:POKE DC+1,DH
1998 AN=AN-1:L=L+1
1999 RETURN
2000 REM *INICIALIZACION*
2010 L=0:AN=0:DIM CO(100)
2015 INPUT"NOMBRE FICHERO";N$
2020 INPUT"DIRECCION DE INICIO";DI
2030 DL=DI AND &HFF
2040 DH=DI/256 - &HFF00
2045 PRINT#2,N$
2050 PRINT#2,,HEX$(DI)," ; ARRAY 1024 BYTES "
2060 D1=DI+1024:FOR I=DI TO D1:POKEI,0:NEXTI
2070 PRINT#2,,HEX$(D1),"LD HL,";HEX$(DI),"21 ";HEX$(DL);" ";HEX$(DH)
2080 POKE D1,&H21:D1=D1+1
2090 POKE D1,DL:D1=D1+1
2100 POKE D1,DH:D1=D1+1
2110 RETURN
8999 REM ********* LEER DE TECLADO *******
9000 DATA &HCD,&H9F,&H00,&HFE,&H1B,&H20,&H01,&HAF,&H77
9010 REM ********* ESCRIBIR A PANTALLA ******
9020 DATA &H7E,&HFE,&H0A,&H20,&H05,&HCD,&HA2,&H00,&H3E,&H0D,&HCD,&HA2,&H00
10000 PRINT
10010 IF ERR=999 THEN PRINT"CORCHETE MISMATCH EN POSICION ";L
10020 IF ERR=998 THEN PRINT"CORCHETE MISMATCH"
10030 CLOSE#1:CLOSE#2
10040 ON ERROR GOTO 0
10050 END
