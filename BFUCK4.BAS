10 SCREEN 0:WIDTH 80
15 NC=999
20 DIM VC(NC):FOR I=0 TO NC:VC(I)=0:NEXT I
30 PT=0:TX=0
40 GOSUB 1000 
50 GOTO 600
100 REM ****** INSTRUCCION > *************
110 BI=BI+1:PT=PT+1:IF PT>NC THEN PT=0
120 RETURN
130 REM ****** INSTRUCCION < *************
140 BI=BI+1:PT=PT-1:IF PT<0 THEN PT=NC
150 RETURN
160 REM ****** INSTRUCCION + *************
170 BI=BI+1:VC(PT)=(VC(PT)+1)MOD256
180 RETURN
190 REM ****** INSTRUCCION - *************
200 BI=BI+1:VC(PT)=(VC(PT)+255)MOD256
210 RETURN
220 REM ****** BUCLE [  *************
230 IF VC(PT)>0 THEN BI=BI+1:RETURN
240 AN=1:BI=BI+1
250 K=PEEK(&HA800+BI)
260 IF K=91 THEN AN=AN+1:GOTO 280
270 IF K=93 THEN AN=AN-1:IF AN=0 THEN BI=BI+1:RETURN
280 BI=BI+1:GOTO 250
290 REM ****** BUCLE ]  *************
300 IF VC(PT)=0 THEN BI=BI+1:RETURN
310 AN=1:BI=BI-1
320 K=PEEK(&HA800+BI)
330 IF K=93 THEN AN=AN+1:GOTO 350
340 IF K=91 THEN AN=AN-1:IF AN=0 THEN BI=BI+1:RETURN
350 BI=BI-1:GOTO 320
500 REM ******* INSTRUCCION . *********
510 BI=BI+1:CC=VC(PT)
515 IF CC=10 OR CC=0 OR CC=13 THEN TX=(INT(TX/80)+1)*80 ELSE VPOKETX,VC(PT):TX=TX+1
520 RETURN
530 REM ***** INSTRUCCION , ********
540 A$=INKEY$:IF A$="" THEN 540
550 IF ASC(A$)=27 OR ASC(A$)=13 THEN VC(PT)=0 ELSE VC(PT)=ASC(A$)
560 BI=BI+1:RETURN
600 REM ****** EJECUTAR PROGRAMA *******
610 CLS
620 IF L<BI THEN LOCATE 1,20:END
630 LOCATE1,19:PRINT PT;BI;
640 O=PEEK(&hA800+BI)
650 IF O=62 THEN GOSUB 110: GOTO 620
660 IF O=60 THEN GOSUB 140: GOTO 620
670 IF O=43 THEN GOSUB 170: GOTO 620
680 IF O=45 THEN GOSUB 200: GOTO 620
690 IF O=91 THEN GOSUB 230: GOTO 620
700 IF O=93 THEN GOSUB 300: GOTO 620
710 IF O=46 THEN GOSUB 510: GOTO 620
720 IF O=44 THEN GOSUB 540: GOTO 620
730 BI=BI+1:GOTO 620
1000 REM *** CARGAR PROGRAMA ***
1010 OPEN "A:PROG.BF" FOR INPUT AS #1
1020 BI=0:L=0
1030 IF EOF(1) THEN 1110
1040 A$=INPUT$(1,1)
1080 IF A$=">"OR A$="<"OR A$="+"OR A$="-"OR A$="["OR A$="]"OR A$="."OR A$="," THEN POKE &HA800+BI,ASC(A$):BI=BI+1:L=L+1:PRINTA$;
1100 GOTO 1030
1110 CLOSE#1:BI=0:PRINT:PRINT"LONGITUD:";L:PRINT"PULSA UNA TECLA PARA EJECUTAR"
1120 A$=INKEY$:IF A$="" GOTO 1120
1130 RETURN
10000 FOR I=0 TO NC:PRINT VC(I);:NEXTI:RETURN
